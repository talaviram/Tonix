message("Creating CPack...")
set(CPACK_PACKAGE_NAME ${PROJECT_NAME})
set(CPACK_PACKAGE_VERSION ${PROJECT_VERSION})

set(PLUGIN_TARGETS AAX CLAP VST3)

get_target_property(CLAP_ARTEFACT "${PLUGIN_NAME}_CLAP" JUCE_PLUGIN_ARTEFACT_FILE)
get_target_property(VST3_ARTEFACT "${PLUGIN_NAME}_VST3" JUCE_PLUGIN_ARTEFACT_FILE)

if(NOT LINUX)
    get_target_property(AAX_ARTEFACT "${PLUGIN_NAME}_AAX" JUCE_PLUGIN_ARTEFACT_FILE)
endif()

set(CPACK_RESOURCE_FILE_README "${CMAKE_SOURCE_DIR}/cmake/BINARY_README.txt")
set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_BINARY_DIR}/LICENSE.txt")

if(APPLE)
    set(CPACK_SET_DESTDIR FALSE)
    set(CPACK_PACKAGING_INSTALL_PREFIX "/")
    set(CPACK_GENERATOR productbuild)
    get_target_property(AU_ARTEFACT "${PLUGIN_NAME}_AU" JUCE_PLUGIN_ARTEFACT_FILE)
    set(AU_CPACK_PLUGIN_PACKAGE_DESTINATION "Library/Audio/Plug-ins/Components")
    install(DIRECTORY ${AU_ARTEFACT}
        DESTINATION ${AU_CPACK_PLUGIN_PACKAGE_DESTINATION}
        COMPONENT AU)
    list(APPEND PLUGIN_TARGETS AU)
    set(AAX_CPACK_PLUGIN_PACKAGE_DESTINATION "Library/Application Support/Avid/Audio/Plug-ins")
    set(CLAP_CPACK_PLUGIN_PACKAGE_DESTINATION "Library/Audio/Plug-ins/CLAP")
    set(VST3_CPACK_PLUGIN_PACKAGE_DESTINATION "Library/Audio/Plug-ins/VST3")

    set(CPACK_PRODUCTBUILD_IDENTIFIER ${BUNDLE_ID})
    message(${BUNDLE_ID})
    # set(MAC_IDENTITY) TODO
    set(CPACK_PRODUCTBUILD_IDENTITY_NAME ${MAC_IDENTITY})
    set(CPACK_PKGBUILD_IDENTITY_NAME ${MAC_IDENTITY})
elseif(WIN32)
    set(CPACK_GENERATOR WIX)
    set(CPACK_WIX_PRODUCT_GUID "BF38C9F1-48FC-4379-BA90-2B0CC502D4A7")
    set(CPACK_WIX_UPGRADE_GUID "9C8A98C7-B641-463F-99B8-CF4E5F47D1EB")
    set(CPACK_WIX_SKIP_PROGRAM_FOLDER TRUE)
    set(CPACK_PACKAGE_INSTALL_DIRECTORY "Program Files/Common Files")
    set(AAX_CPACK_PLUGIN_PACKAGE_DESTINATION "Avid/Audio/Plug-ins")
    set(CLAP_CPACK_PLUGIN_PACKAGE_DESTINATION "CLAP")
    set(VST3_CPACK_PLUGIN_PACKAGE_DESTINATION "VST3")
elseif(LINUX)
    set(CPACK_GENERATOR "TGZ")
    set(CPACK_ARCHIVE_COMPONENT_INSTALL ON)
    set(CPACK_COMPONENTS_GROUPING ALL_COMPONENTS_IN_ONE)
    set(CLAP_CPACK_PLUGIN_PACKAGE_DESTINATION "usr/local/lib/clap/")
    set(VST3_CPACK_PLUGIN_PACKAGE_DESTINATION "usr/local/lib/vst3/")
endif()

if (NOT LINUX)
    install(DIRECTORY ${AAX_ARTEFACT}
        DESTINATION ${AAX_CPACK_PLUGIN_PACKAGE_DESTINATION}
        COMPONENT AAX)
endif()

install(DIRECTORY ${CLAP_ARTEFACT}
    DESTINATION ${CLAP_CPACK_PLUGIN_PACKAGE_DESTINATION}
    COMPONENT CLAP)    

install(DIRECTORY ${VST3_ARTEFACT}
    DESTINATION ${VST3_CPACK_PLUGIN_PACKAGE_DESTINATION}
    COMPONENT VST3)

set(CPACK_COMPONENTS_ALL ${PLUGIN_TARGETS})
set(CPACK_PACKAGE_VENDOR ${COMPANY_NAME})

set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "${JUCE_DESCRIPTION} Installer")
include(CPack)

if(NOT LINUX)
    cpack_add_component(AAX DISPLAY_NAME "AAX"
        DESCRIPTION
        "Install AAX Plug-In Format"
    )
endif()

if(APPLE)
    cpack_add_component(AU DISPLAY_NAME "Audio-Unit"
        DESCRIPTION
        "Install Audio Unit Plug-In Format"
    )
endif()

cpack_add_component(CLAP DISPLAY_NAME "CLAP"
    DESCRIPTION
    "Install CLAP Plug-In Format"
)

cpack_add_component(VST3 DISPLAY_NAME "VST3"
    DESCRIPTION
    "Install VST3 Plug-In Format"
)
